;; Function to return an empty address
slice addr_none() asm "b{00} PUSHSLICE";

;; ========== Gas Constants ==========
;; --- Basic Processing ---
;; Gas consumption for basic data processing (0.005 TON)
int gas_basic_processing_consumption() asm "5000000 PUSHINT";
;; Gas consumption for providing contract address (0.01 TON)
int provide_address_gas_consumption() asm "10000000 PUSHINT";

;; --- Storage Operations ---
;; Gas consumption for loading storage (0.005 TON)
int gas_storage_load_consumption() asm "5000000 PUSHINT";
;; Gas consumption for saving storage (0.005 TON)
int gas_storage_save_consumption() asm "5000000 PUSHINT";
;; Gas consumption for updating dictionary (0.005 TON)
int gas_dict_update_consumption() asm "5000000 PUSHINT";

;; --- Excess Gas ---
;; Gas consumption for returning excess gas (0.005 TON)
int gas_excess_return_consumption() asm "5000000 PUSHINT";
;; Minimum excess threshold (0.001 TON)
int min_excess_threshold() asm "1000000 PUSHINT";

;; --- Jetton :: Minting ---
;; Gas consumption for jetton transfer message (0.05 TON)
int gas_jetton_transfer_consumption() asm "50000000 PUSHINT";



;; Function to update swaps dict
() update_swaps_info(int swap_id, slice user_address, int index_amount, int received_excesses, int excess_gas, int timestamp) impure {
    storage::dict_swaps_info~udict_set_builder(
        64,
        swap_id,
        begin_cell()
            .store_slice(user_address)
            .store_coins(index_amount)
            .store_uint(received_excesses, 8)
            .store_coins(excess_gas)
            .store_uint(timestamp, 32)
    );
}

;; Function to load swaps info from dict
(slice, int, int, int, int) load_swaps_info(int swap_id) impure {
    (slice swap_data, int found) = storage::dict_swaps_info.udict_get?(64, swap_id);
    if (found) {
        int refs = swap_data.slice_refs();
        if (refs > 0) {
            cell ref_cell = swap_data~load_ref();
            swap_data = ref_cell.begin_parse();
        }
        
        ;; Load user address, amount, received excesses, excess gas, and timestamp
        slice user_address = swap_data~load_msg_addr();
        int index_amount = swap_data~load_coins();
        int received_excesses = swap_data~load_uint(8);
        int excess_gas = swap_data~load_coins();
        int timestamp = swap_data~load_uint(32);
        return (user_address, index_amount, received_excesses, excess_gas, timestamp);
    }
    return (null(), 0, 0, 0, 0);
}

() send_jetton(
    int swap_id,
    slice jetton_wallet_address,
    int gas,
    int jetton_amount,
    slice to_address
) impure {
    cell body = begin_cell()
        .store_uint(op::transfer(), 32)
        .store_uint(swap_id, 64)
        .store_coins(jetton_amount)
        .store_slice(to_address)
        .store_slice(to_address)
        .store_uint(0, 1)
        .store_coins(0)
        .store_uint(0, 1)
        .end_cell();
    cell msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(jetton_wallet_address)
        .store_coins(gas)
        .store_uint(1, 107)
        .store_ref(body)
        .end_cell();
    send_raw_message(msg, 0);
}